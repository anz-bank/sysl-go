CodeGenTransform:
  !view filename(app <: sysl.App) -> string:
    app -> (:
      filename =  "app.go"
    )

  !view makeValueExpr(strValue <: string) -> Expression:
    strValue -> (:
      ValueExpr = strValue
    )

  !view makeMethodCallExpr(app <: sysl.App, module <: sysl.Module, expr1 <: string, expr2 <: string) -> Expression:
    app -> (:
      let clientPkgs = makeClientPkgList(app, module) flatten(.out) -> <set of string>(pkg:
        out = pkg + "Client"
      )      
      ValueExpr = "NewServiceHandler(" + expr1 + "," + expr2 + "," + Join(clientPkgs flatten(.out), ",") + ")"
    )

  !view makeInitialiseHandler(app <: sysl.App, module <: sysl.Module) -> TopLevelDecl:
    app -> (:
      let typeName = "*HandlerManager"
      Comment = '// InitialiseHandler ...'
      MethodDecl = app -> <MethodDecl>(:
        Receiver = app -> <Receiver>(:
          ReceiverType = "a *App"
        )
        FunctionName = "InitialiseHandler"
        Signature = app -> <Signature>(:
          Parameters = app -> <Parameters>(:
            ParameterList = app -> <ParameterList>(:
              ParameterDecl = app -> <ParameterDecl>(:
                Identifier = "h"
                TypeName = typeName
              )
              let serviceInterface = app -> <ParameterDeclC>(:
                ParameterDecl = app -> <ParameterDecl>(:
                  Identifier = "serviceInterface"
                  TypeName = "ServiceInterface"
                )
              )
              let serviceCallback = app -> <ParameterDeclC>(:
                ParameterDecl = app -> <ParameterDecl>(:
                  Identifier = "serviceCallback"
                  TypeName = "config.Callback"
                )
              )
              ParameterDeclC = [serviceInterface] |  [serviceCallback]
            )
          )
          Result = app -> <Result>(:
            TypeName = "error"
          )
        )
        Block = app -> <Block>(:      
          let clientPkgs = makeClientPkgList(app, module) flatten(.out) -> <set of string>(pkg:
            pkgHttpClientStmt = pkg -> <StatementList> (:
              Statement = pkg -> <Statement>(:
                DeclareAndAssignStmt = pkg -> <DeclareAndAssignStmt> (:
                  Variables = pkg + "HTTPClient" + ",  "+ ToLower(pkg) + "Err"
                  Expression = makeValueExpr("core.BuildDownstreamHTTPClient(\"" + ToLower(pkg) + "\", &h.coreCfg.GenCode.Downstream.(*DownstreamConfig)." + Title(ToLower(pkg)) + ")")
                )
              )
            )
            ifStmt = pkg -> <StatementList>(:
              Statement = pkg -> <Statement>(:
                IfElseStmt = pkg -> <IfElseStmt> (:
                  Expression = makeValueExpr(ToLower(pkg) + "Err" + " != nil")
                  Block = pkg -> <Block> (:
                    let stmt = pkg -> <StatementList> (:
                      Statement = pkg -> <Statement> (:
                        ReturnStmt = pkg -> <ReturnStmt> (:
                          PayLoad = ToLower(pkg) + "Err"
                        )
                      )
                    )
                    StatementList = [stmt]
                  )
                )
              )
            )
            pkgClientStmt = pkg -> <StatementList> (:
              Statement = pkg -> <Statement>(:
                DeclareAndAssignStmt = pkg -> <DeclareAndAssignStmt> (:
                  Variables = ToLower(pkg) + "Client"
                  Expression = makeValueExpr(ToLower(pkg) + ".NewClient(" + ToLower(pkg) + "HTTPClient, h.coreCfg.GenCode.Downstream.(*DownstreamConfig)." + Title(ToLower(pkg)) + ".ServiceURL)")
                )
              )
            )
          )

          let serviceHandlerStmt = app -> <StatementList> (:
            Statement = app -> <Statement>(:
              DeclareAndAssignStmt = app -> <DeclareAndAssignStmt> (:
                Variables = "serviceHandler"
                Expression = makeMethodCallExpr(app, module, "genCallbacks", "&bffSI")
              )
            )
          )

          let serviceRouterStmt = app -> <StatementList> (:
            Statement = app -> <Statement>(:
              DeclareAndAssignStmt = app -> <DeclareAndAssignStmt> (:
                Variables = "serviceRouter"
                Expression = makeValueExpr("NewServiceRouter(genCallbacks, serviceHandler)")
              )
            )
          )

          let enabledHandlersStmt = app -> <StatementList> (:
            Statement = app -> <Statement>(:
              AssignStmt = app -> <AssignStmt> (:
                Variables = "h.enabledHandlers"
                Expression = makeValueExpr("append(h.enabledHandlers, serviceRouter)")
              )
            )
          )

          let retStmt = app -> <StatementList>(:
            Statement = app -> <Statement>(:
              ReturnStmt = app -> <ReturnStmt>(:
                PayLoad = "nil"
              )
            )
          )
          StatementList = [clientPkgs flatten(.pkgHttpClientStmt)] | clientPkgs flatten(.ifStmt) | clientPkgs flatten(.pkgClientStmt) | [serviceHandlerStmt, serviceRouterStmt, enabledHandlersStmt, retStmt]
        )
      )
    )

  !view makeClientPkgList(app <: sysl.App, module <: sysl.Module) -> set of string:
    app.endpoints -> (ep:
      let callList = ep.value.stmts where (.type == "call") -> <set of out> (call:
        let depList = module.apps where(.value.name == call.target) -> <set of out> (dep:
          out = dep.value.attrs.package
        )
        out = depList flatten(.out)
      )
      out = callList flatten(.out)
    )   

  !view goFile(app <: sysl.App, module <: sysl.Module, basePath <: string) -> goFile:
    app -> (:
      PackageClause = app -> <PackageClause> (:
        let pname = if .attrs.package != null then .attrs.package else ToLower(app.name)
        PackageName = ToLower(pname)
      )

      ImportDecl = app -> <ImportDecl>(:
        let config = "github.com/anz-bank/sysl-go/config"
        let core = "github.com/anz-bank/sysl-go/core"
        let handlerinitialiser = "github.com/anz-bank/sysl-go/handlerinitialiser"
        let clientImports = makeClientPkgList(app, module) flatten(.out) -> <set of string>(i:
          out = basePath + "/" + i
        )
        let spec = ["context", config, core, handlerinitialiser] -> <sequence of ImportSpec> (importPath:
          Import = if importPath == "" then true else '"' + importPath + '"'
        )
        let ci = clientImports flatten(.out) -> <sequence of ImportSpec> (importPath:
          Import = if importPath == "" then true else '"' + importPath + '"'
        )
        ImportSpec = spec | ci
      )

      let appStruct = [.name] -> <sequence of TopLevelDecl> (name:
        Comment = "// App for " + name
        Declaration = name -> <Declaration>(:
          StructType = name -> <StructType>(:
            StructName = "App"
          )
        )
      )

      let handlerManagerStruct = [.name] -> <sequence of TopLevelDecl> (name:
        Comment = "// HandlerManager for " + name
        Declaration = name -> <Declaration>(:
          StructType = name -> <StructType>(:
            StructName = "HandlerManager"
            let coreCfg = name -> <FieldDecl>(:
              identifier = "coreCfg"
              Type =  "*config.DefaultConfig"
            )

            let enabledHandlers = name -> <FieldDecl>(:
              identifier = "enabledHandlers"
              Type = "[]handlerinitialiser.HandlerInitialiser"
            )

            let enabledGrpcHandlers = name -> <FieldDecl>(:
              identifier = "enabledGrpcHandlers"
              Type = "[]handlerinitialiser.GrpcHandlerInitialiser"
            )
            FieldDecl = [coreCfg, enabledHandlers, enabledGrpcHandlers]
          )
        )
      )
      Comment = "// Code generated by sysl DO NOT EDIT.\n"
      TopLevelDecl = appStruct | handlerManagerStruct | [makeInitialiseHandler(app, module)]
    )
