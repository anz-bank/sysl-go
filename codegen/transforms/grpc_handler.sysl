CodeGenTransform:
  !view GoName(word <: string) -> string:
    word -> (:
      let splitted = if MatchString("^[\\p{L}_][\\p{L}\\p{N}_]*$", word) then [word] else FindAllString("[\\p{L}_][\\p{L}\\p{N}_]*", word, -1)

      # Common initialisms https://github.com/golang/lint/blob/8f45f776aaf18cebc8d65861cc70c33c60471952/lint.go#L771
      let initialisms = ["ACL", "API", "ASCII", "CPU", "CSS", "DNS", "EOF", "GUID", "HTML", "HTTP", "HTTPS", "ID", "IP", "JSON", "LHS", "QPS", "RAM", "RHS", "RPC", "SLA", "SMTP", "SQL", "SSH", "TCP", "TLS", "TTL", "UDP", "UI", "UID", "UUID", "URI", "URL", "UTF8", "VM", "XML", "XMPP", "XSRF", "XSS"]

      let wordUpper = splitted -> <sequence of string> (w:
        let upper = ToTitle(TrimPrefix(w,"_"))
        out = if upper in initialisms then upper else Title(w)
      )
      out = Join(wordUpper flatten(.out), "")
    )

  !view ParamType(t <: sysl.Type) -> string:
    t -> (:
      let IsPtr = if t.optional == true && t.type != "sequence" then "*" else ""
      let typeName = if t.type ==:
        "primitive" => if t.primitive ==:
          "DECIMAL" => "double"
          "INT" => "int64"
          "FLOAT" => "float64"
          "STRING" => "string"
          "STRING_8" => "string"
          "BOOL" => "bool"
          "DATE" => "date.Date"
          "DATETIME" => "time.Time"
        "sequence" => "[]" + GoType(t.sequence).out
        else GoName(t.type_ref).out
      out = IsPtr + typeName
    )

  !view filename(app <: sysl.App) -> string:
    app -> (:
      filename =  "grpc_handler.go"
    )

  !view makeValueExpr(strValue <: string) -> Expression:
    strValue -> (:
      ValueExpr = strValue
    )

  !view paramDecl(identifier <: string, typeName <: string) -> ParameterDecl:
    identifier -> (:
      Identifier = identifier
      TypeName = typeName
    )

  !view varDecl(name <: string, typeName <: string) -> VarDecl:
    name -> (:
      identifier = name
      TypeName = typeName
    )

  !view handleError(condition <: string, errorCode <: string, errorMsg <: string, errorObj <: string) -> StatementList:
    condition -> (:
      Statement = condition -> <Statement> (:
        IfElseStmt = condition -> <IfElseStmt> (:
          Expression = makeValueExpr(condition)
          Block = condition -> <Block> (:
            let interfaceCall = condition -> <StatementList> (:
              Statement = condition -> <Statement> (:
                FunctionCall = condition -> <ReturnStmt> (:
                  FunctionName = "s.genCallback.HandleError"
                  FunctionArgs = condition -> <FunctionArgs> (:
                    Expression = makeValueExpr("ctx")
                    let writer = condition -> <FuncArgsRest> (:
                      Expression = makeValueExpr("w")
                    )
                    let code = errorCode -> <FuncArgsRest> (:
                      Expression = makeValueExpr(errorCode)
                    )
                    let msg = errorMsg -> <FuncArgsRest>(:
                      Expression = makeValueExpr(errorMsg)
                    )
                    let error = errorObj -> <FuncArgsRest> (:
                      Expression = makeValueExpr(errorObj)
                    )
                    FuncArgsRest = [writer, code, msg, error]
                  )
                )
              )
            )

            let returnStmt = condition -> <StatementList> (:
              Statement = condition -> <Statement> (:
                ReturnStmt = condition -> <ReturnStmt> (:
                  PayLoad = ""
                )
              )
            )
            StatementList = [interfaceCall, returnStmt]
          )
        )
      )
    )

  !view structVarDecl(bodyParams <: BodyParams) -> sequence of StatementList:
    bodyParams -> (param:
      Statement = param -> <Statement>(:
        VarDecl =varDecl(param.paramName, param.paramType)
      )
    )

  !view jsonDecode(bodyParams <: BodyParams, packageName <: string) -> Block:
    packageName -> (:
      let decodeErr = "decodeErr"

      let decodeStmts = bodyParams -> <sequence of StatementList> (param:
        let decode = param -> <Statement> (:
          DeclareAndAssignStmt = param -> <DeclareAndAssignStmt> (:
            Variables = decodeErr
            Expression = param -> <Expression> (:
              FunctionCall = param -> <FunctionCall> (:
                FunctionName = "decoder.Decode"
                FunctionArgs = param -> <FunctionArgs> (:
                  Expression = makeValueExpr("&" + param.paramName)
                )
              )
            )
          )
        )
        Statement = decode
      )

      StatementList = decodeStmts | [handleError(decodeErr + " != nil", "http.StatusBadRequest", '"Error reading request body"', decodeErr)]
    )

  !view methodDef(ep <: sysl.Endpoint) -> string:
    ep -> (:
      let terms = if ep.value.is_rest then ep.value.pathvars else ["x"] where (. == "y") -> <out> (:
        out = "{" + .name + "}"
      )
      let pathVars = terms flatten(.out)
      let path = if ep.value.is_rest then Split(ep.value.path, "/") else [""]
      let method = if ep.value.is_rest then Title(ToLower(ep.value.method)) else ep.value.name

      let methodPostfix = path -> <sequence of string> (p:
        let postfix  = if p in pathVars then "" else p
        out = Title(ToLower(postfix))
      )

      let withArg = if ep.value.is_rest then if MatchString("\\{\\p{L}+\\}$", ep.value.path) && Contains("POST", ToUpper(method)) then "WithArg" else "" else ""
      let getList = if ep.value.is_rest then if MatchString("[\\p{L}\\p{N}]$", ep.value.path) && Contains("GET", ToUpper(method)) then "List" else "" else ""

      methodName = GoName(method + Join(methodPostfix flatten(.out), "") + withArg + getList).out
    )

  !view getParamStatements(paramList <: ParamList, methodName <: string) -> sequence of StatementList:
    paramList -> (param:
      Statement = param -> <Statement> (:
        DeclareAndAssignStmt = param -> <DeclareAndAssignStmt> (:
          Variables = param.varName
          Expression = param -> <Expression> (:
            FunctionCall = param -> <FunctionCall> (:
              FunctionName = methodName
              FunctionArgs = param -> <FunctionArgs> (:
                Expression = makeValueExpr("r")
                FuncArgsRest = [param] -> <FuncArgsRest> (:
                  Expression = makeValueExpr('"' + param.paramName + '"')
                )
              )
            )
          )
        )
      )
    )

  !view getOptParamStatements(paramList <: ParamList, methodName <: string) -> Block:
    methodName -> (:
      let paramSuffix = paramList -> <sequence of ParamList> (p:
        varName = p.varName + "Param"
        paramName = p.paramName
      )
      let assignStmt = getParamStatements(paramSuffix, methodName)

      let assignVar = paramList -> <sequence of StatementList> (param:
        Statement = param -> <Statement> (:
          DeclareAndAssignStmt = param -> <DeclareAndAssignStmt> (:
            Variables = param.varName
            let conversion = if param.paramType ==:
              "*bool" => "convert.StringToBoolPtr"
              else "convert.StringToStringPtr"

            Expression = makeValueExpr(conversion + "(" + param.varName + "Param)")
          )
        )
      )
      
      StatementList = assignStmt | assignVar
    )

  !view validateHeaders(ep <: sysl.Endpoint) -> sequence of StatementList:
    ep.value.params where("header" in .attrs.patterns && "required" in .attrs.patterns) -> (param:
      let condition = GoName(param.attrs.name).out + ' == ""'
      let errorCode = "http.StatusBadRequest"
      let errMsg = '"' + param.attrs.name + ' header length is zero"'
      Statement = handleError(condition, errorCode, errMsg, "nil").Statement
    )

  !view declareHeaders(variableName <: string, typeName <: string, values <: sequence of string) -> StatementList:
    typeName -> (:
      Statement = typeName -> <Statement> (:
        DeclareAndAssignStmt = typeName -> <DeclareAndAssignStmt> (:
          Variables = variableName
          Expression = typeName -> <Expression> (:
            NewSlice = typeName -> <NewSlice> (:
              TypeName = typeName
              let v = values -> <sequence of string> (vv:
                out = '"' + vv + '"'
              )
              SliceValues = Join(v flatten(.out), ',')
            )
          )
        )
      )
    )

  !view getBodyParams(bodyParams <: BodyParams, packageName <: string) -> Block:
    packageName -> (:
      let decoder = jsonDecoder("decoder")
      let structVarDecl = structVarDecl(bodyParams)
      let jsonDecode = jsonDecode(bodyParams, packageName).StatementList

      StatementList = if bodyParams count > 0 then [decoder] | structVarDecl | jsonDecode else structVarDecl
    )

  !view queryParamNameFromAttrs(ep <: sysl.Endpoint, qparam <: sysl.EndPoint.RestParams.QueryParam) -> string:
    ep -> (:
      let matchingPname = .value.attrs.queryParamNames where (qparam.name in .) -> <sequence of string> (namePair:
          mpout = namePair where (. != qparam.name)
      )
      let matchingPnameAsString = matchingPname flatten(.mpout) -> <sequence of string> (v:
        mpasout = Join(v,"")
      )

      out = Join(matchingPnameAsString flatten(.mpasout), "")
    )
    
  !view queryParamName(ep <: sysl.Endpoint, qparam <: sysl.EndPoint.RestParams.QueryParam) -> string:
    ep -> (:
      out = if "queryParamNames" in .value.attrs then queryParamNameFromAttrs(ep, qparam).out else qparam.name
    )

  !view getPackagePrefix(payload <: string, module <: sysl.Module) -> string:
    payload -> (:
      let packages = Split(payload, ".") -> <set of string>(p:
        let depList = module.apps where(.value.name == p) -> <set of string> (dep:
          pkg = getPackage(dep.value).pname
          appName = p
        )
        pkg = depList flatten(.pkg)
        appName = depList flatten(.appName)
      )
      out = if packages flatten(.pkg) count == 0 then "" else packages flatten(.pkg) single + "."
      appName = if packages flatten(.appName) count == 0 then "" else packages flatten(.appName) single
    )

  !view getAllParams(ep <: sysl.Endpoint, module <: sysl.Module) -> AllParams:
    ep -> (:
      let reqQueryParams = if ep.value.is_rest then ep.value.queryvars where (.optional == false) else ["x"] where (. == "y") -> <QueryParams> (:
        paramType = ParamType(.).out
        paramName = queryParamName(ep, .).out
        varName = GoName(.name).out
      )
      let optQueryParams = if ep.value.is_rest then ep.value.queryvars where (.optional == true) else ["x"] where (. == "y") -> <QueryParams> (:
        paramType = ParamType(.).out
        paramName = queryParamName(ep, .).out
        varName = GoName(.name).out
      )
      let pathParams = if ep.value.is_rest then ep.value.pathvars else ["x"] where (. == "y") -> <PathParams> (:
        paramType = ParamType(.).out
        paramName = .name
        varName = GoName(.name).out
      )
      let reqHeaderParams = if ep.value.is_rest then ep.value.params where("header" in .attrs.patterns && "required" in .attrs.patterns) else ["x"] where (. == "y") -> <HeaderParams> (:
        paramType = ParamType(.).out
        paramName = .attrs.name
        varName = GoName(.attrs.name).out
        isOptional = false
      )
      let optHeaderParams = if ep.value.is_rest then ep.value.params where("header" in .attrs.patterns && "optional" in .attrs.patterns) else ["x"] where (. == "y") -> <HeaderParams> (:
        paramType = ParamType(.).out
        paramName = .attrs.name
        varName = GoName(.attrs.name).out
        isOptional = true
      )
      let bodyParams = if ep.value.is_rest then ep.value.params where("body" in .attrs.patterns) else ep.value.params -> <BodyParams> (:
        let p = getPackagePrefix(.type_ref, module)
        let t = TrimPrefix(.type_ref, p.appName)
        paramType = p.out + GoName(t).out
        paramName = GoName(.name).out
        varName = GoName(.name).out
      )
      QueryParams = reqQueryParams | optQueryParams
      ReqQueryParams = reqQueryParams
      OptQueryParams = optQueryParams
      PathParams = pathParams
      ReqHeaderParams = reqHeaderParams
      OptHeaderParams = optHeaderParams
      HeaderParams = reqHeaderParams | optHeaderParams
      BodyParams = bodyParams
      AllParams = pathParams flatten(.paramName) | reqQueryParams flatten(.paramName) |  optQueryParams flatten(.paramName) | reqHeaderParams flatten(.paramName) | optHeaderParams flatten(.paramName) | bodyParams flatten(.paramName)
    )

  !view jsonDecoder(decoderName <: string) -> StatementList:
    decoderName -> (:
      Statement = decoderName -> <Statement>(:
        DeclareAndAssignStmt = decoderName -> <DeclareAndAssignStmt>(:
          Variables = decoderName
          Expression = decoderName -> <Expression> (:
            FunctionCall = decoderName -> <FunctionCall> (:
              FunctionName = 'json.NewDecoder'
              FunctionArgs = decoderName -> <FunctionArgs> (:
                Expression = makeValueExpr("r.Body")
              )
            )
          )
        )
      )
    )

  !view makeFunctionCall(funcName <: string, arg0 <: ValueExpr, rest <: sequence of ValueExpr) -> StatementList:
    funcName -> (:
      Statement = funcName -> <Statement>(:
        FunctionCall = funcName -> <FunctionCall>(:
          FunctionName = funcName
          FunctionArgs = funcName -> <FunctionArgs> (:
            Expression = arg0
            FuncArgsRest = rest -> <FuncArgsRest> (:
              Expression = .
            )
          )
        )
      )
    )

  !view makeResult(ep <: sysl.Endpoint) -> string:
    ep -> (:
        let tn = ep.value.ret -> <sequence of string>(t:
          let justName = ToLower(TrimPrefix(t.value, "sequence of "))
          let p = if MatchString("^.+\\.", justName) then FindAllString("^.+\\.", justName, 1) single else ""
          out = TrimPrefix(justName, p)
        )
        out = Join(tn flatten(.out), ", ")
    )

  !view makeResultType(ep <: sysl.Endpoint) -> string:
    ep -> (:
        let tn = ep.value.ret -> <sequence of string>(t:
          let justName = GoName(TrimPrefix(t.value, "sequence of ")).out
          let p = if MatchString("^.+\\.", justName) then FindAllString("^.+\\.", justName, 1) single else ""
          out = TrimPrefix(justName, p)
        )
        out = Join(tn flatten(.out), ", ")
    )

  !view HandlerSignature(ep <: sysl.Endpoint, packageName <: string, funcName <: string) -> Signature:
    ep -> (:
      Parameters = ep -> <Parameters>(:
        ParameterList = ep -> <ParameterList>(:
          ParameterDecl = paramDecl("ctx", "context.Context")
          ParameterDeclC = [ep] -> <sequence of ParameterDeclC>(p:
            ParameterDecl = paramDecl("req", "*" + packageName + "." + funcName + "Request")
          )
        )
      )
      Result = ep -> <Result> (:
        ReturnTypes = ep -> <ReturnTypes> (:
          TypeName = "*" + packageName + "." + funcName + "Response"
          ResultTypeList = ["error"] -> <sequence of ResultTypeList>(p:
            TypeName = p
          )
        )
      )
    )

  !view EpHandlerMethodDecl(eps <: sequence of sysl.Endpoints, packageName <: string, module <: sysl.Module) -> sequence of TopLevelDecl:
    eps -> (ep:
      let implFuncName = if ep.value.is_rest then methodDef(ep).methodName else ep.value.name
      let funcName = implFuncName
      let allParams = getAllParams(ep, module)
      Comment = '// ' + funcName + ' ...'
      MethodDecl = ep -> <MethodDecl>(:
        Receiver = ep -> <Receiver>(:
          ReceiverType = "s *GrpcServiceHandler"
        )
        FunctionName = funcName
        Signature = HandlerSignature(ep, packageName, implFuncName)

        Block = ep -> <Block>(:

          # Use the gRPC Unimplemented method set to respond to calls to methods
          # not implemented in this server
          let nilCheck = implFuncName -> <StatementList>(:
            Statement = implFuncName -> <Statement>(:
              IfElseStmt = implFuncName -> <IfElseStmt>(:
                Expression = makeValueExpr("s.serviceInterface." + implFuncName + " == nil")
                Block = implFuncName -> <Block>(:
                    let returnError = implFuncName -> <StatementList>(:
                      Statement = implFuncName -> <Statement>(:
                        ReturnStmt = implFuncName -> <ReturnStmt>(:
                          Expression = makeFunctionCall("s.unimpl." + implFuncName, makeValueExpr("ctx"), [makeValueExpr("req")]).Statement
                        )
                      )
                    )
                    StatementList = [returnError]
                )
              )
            )
          )

          # Populate the client methods used by the Service Interface
          let declareClient = ep -> <StatementList>(:
            Statement = ep -> <Statement>(:
              DeclareAndAssignStmt = ep -> <DeclareAndAssignStmt> (:
                Variables = "client"
                Expression = ep -> <Expression>(:
                  NewStruct = makeClient(ep, module)
                )
              )
            )
          )
          
          # Call the implementation behind the Service Interface
          let implCall = ep -> <StatementList> (:
            Statement = ep -> <Statement> (:
              ReturnStmt = ep -> <ReturnStmt> (:
                Expression = ep -> <Expression> (:
                  FunctionCall = ep -> <FunctionCall> (:
                    FunctionName = "s.serviceInterface." + implFuncName
                    FunctionArgs = ep -> <FunctionArgs> (:
                      Expression = makeValueExpr("ctx")
                      FuncArgsRest = ["req", "client"] -> <sequence of FuncArgsRest>(p:
                        Expression = makeValueExpr(p)
                      )
                    )
                  )
                )
              )
            )
          )

          let withTimeout = ep -> <StatementList> (:
            Statement = ep -> <Statement>(:
              DeclareAndAssignStmt = ep -> <DeclareAndAssignStmt>(:
                Variables = "ctx, cancel"
                Expression = ep -> <Expression>(:
                  FunctionCall = ep -> <FunctionCall>(:
                    FunctionName = "s.genCallback.DownstreamTimeoutContext"
                    FunctionArgs = ep -> <FunctionArgs>(:
                      Expression = ep -> <Expression>(:
                        ValueExpr = "ctx"
                      )
                    )
                  )
                )
              )
            )
          )

          let deferCancel = "defer cancel"

          let cancel = deferCancel -> <StatementList>(:
            Statement = deferCancel -> <Statement>(:
              FunctionCall = deferCancel -> <FunctionCall>(:
                FunctionName = deferCancel
              )
            )
          )

          StatementList = [nilCheck, withTimeout, cancel, declareClient, implCall]
        )
      )
    )

  !view makeEpClientName(ep <: sysl.Endpoint) -> string:
    ep ->(:
      out = methodDef(ep).methodName + "Client"
    )

  !view makeClient(ep <: sysl.Endpoint, module <: sysl.Module) -> NewStruct:
    ep -> (:
      StructName = makeEpClientName(ep).out
      let sf = ep.value.stmts where (.type == "call") -> <sequence of out> (call:
        let depList = module.apps where(.value.name == call.target) -> <sequence of out> (dep:
          let depEpList = dep.value.endpoints where(.value.name == call.endpoint) -> <sequence of out> (depEp:
            identifier = methodDef(depEp).methodName
            Expression = depEp -> <Expression> (:
              ValueExpr = "s." + getPackage(dep.value).pname + dep.value.name + "Service" + "." + methodDef(depEp).methodName
            )
          )
          out = depEpList
        )
        out = depList flatten(.out) flatten(.)
      )
      StructField = sf flatten(.out) flatten(.)
    )

  !view makeClientStruct(app <: sysl.App, module <: sysl.Module) -> set of out:
    app.endpoints -> (ep:
      let callList = ep.value.stmts where (.type == "call") -> <set of out> (call:
        let depList = module.apps where(.value.name == call.target) -> <set of out> (dep:
          identifier = getPackage(dep.value).pname + dep.value.name + "Service"
          Type = getPackage(dep.value).pname + "." + "Service"
        )
        out = depList
      )
      out = callList flatten(.out)
    )

  !view makeClientParam(app <: sysl.App, module <: sysl.Module) -> set of out:
    app.endpoints -> (ep:
      let callList = ep.value.stmts where (.type == "call") -> <set of out> (call:
        let depList = module.apps where(.value.name == call.target) -> <set of out> (dep:
          ParameterDecl = dep -> <ParameterDecl>(:
            Identifier = getPackage(dep.value).pname + dep.value.name + "Service"
            TypeName = getPackage(dep.value).pname + "." + "Service"
          )
        )
        out = depList
      )
      out = callList flatten(.out)
    )

  !view makeClientParamList(app <: sysl.App, module <: sysl.Module) -> set of string:
    app.endpoints -> (ep:
      let callList = ep.value.stmts where (.type == "call") -> <set of out> (call:
        let depList = module.apps where(.value.name == call.target) -> <set of out> (dep:
          out = getPackage(dep.value).pname + dep.value.name + "Service"
        )
        out = depList flatten(.out)
      )
      out = callList flatten(.out)
    )

  !view makeClientImportList(app <: sysl.App, module <: sysl.Module) -> set of string:
    app.endpoints -> (ep:
      let callList = ep.value.stmts where (.type == "call") -> <set of out> (call:
        let depList = module.apps where(.value.name == call.target) -> <set of out> (dep:
          out = if "go_package" in dep.value.attrs then dep.value.attrs.go_package else getPackage(dep.value).pname
        )
        out = depList flatten(.out)
      )
      out = callList flatten(.out) 
    )

  !view goFile(app <: sysl.App, module <: sysl.Module, depPath <: string) -> goFile:
    app -> (:
      let packageName = if "go_package" in .attrs then .attrs.go_package else if "package" in .attrs then ToLower(.attrs.package) else ToLower(app.name)
      let grpcPkgName = "pb"
      let callbackPkgName = "cb"

      PackageClause = app -> <PackageClause> (:
        PackageName = packageName
      )

      ImportDecl = app -> <ImportDecl>(:
        let convert = "github.com/anz-bank/sysl-go/convert"
        let common = "github.com/anz-bank/sysl-go/common"
        let clientImports = makeClientImportList(app, module) flatten(.out) -> <set of string>(i:
          out = depPath + i
        )

        let goPbPackage = if "go_pb_package" in .attrs then .attrs.go_pb_package else "pb"

        let grpcDefs = grpcPkgName -> <ImportSpec>(:
          NamedImport = grpcPkgName -> <NamedImport>(:
            Name = grpcPkgName
            Import = '"' + depPath + "/" + goPbPackage + '"'
          )
        )

        let callbackDefs = callbackPkgName -> <ImportSpec>(:
          NamedImport = callbackPkgName -> <NamedImport>(:
            Name = callbackPkgName
            Import = '"' + depPath + "/callback" + '"'
          )
        )

        let spec = ["context", common, convert, "google.golang.org/grpc"] | clientImports flatten(.out) -> <sequence of ImportSpec> (importPath:
          Import = if importPath == "" then true else '"' + importPath + '"'
        )
        ImportSpec = spec | [grpcDefs, callbackDefs]
      )

      let serviceImplVarname = "serviceInterface"


      let serviceHandlerStruct = [.name] -> <sequence of TopLevelDecl> (name:
        Comment = "// GrpcServiceHandler for " + name + " API"
        Declaration = name -> <Declaration>(:
          StructType = name -> <StructType>(:
            StructName = "GrpcServiceHandler"

            let genCallback = name -> <FieldDecl>(:
              identifier = "genCallback"
              Type =  callbackPkgName + ".GenCallback"
            )

            let svcHandlerField = name -> <FieldDecl>(:
              identifier = serviceImplVarname
              Type = "*GrpcServiceInterface"
            )

            let unimpl = name -> <FieldDecl>(:
              identifier = "unimpl"
              Type = "*" + grpcPkgName + ".Unimplemented" + app.name + "Server"
            )

            let clientStruct = makeClientStruct(app, module) flatten(.out) -> <set of out>(c:
              out = c 
            )

            FieldDecl = [genCallback, svcHandlerField, unimpl] | clientStruct flatten(.out)
          )
        )
      )

      let newServiceHandler = [.name] -> <sequence of TopLevelDecl> (name:
        Comment = "// NewGrpcServiceHandler for " + name
        FunctionDecl = name -> <FunctionDecl>(:
          FunctionName = "NewGrpcServiceHandler"
          Signature = name -> <Signature> (:
            Parameters = name -> <Parameters>(:
              ParameterList = name -> <ParameterList>(:
                ParameterDecl = paramDecl("genCallback", callbackPkgName + ".GenCallback")
                let genCallback = name -> <ParameterDeclC>(:
                  ParameterDecl = name -> <ParameterDecl>(:
                    Identifier = serviceImplVarname
                    TypeName = "*GrpcServiceInterface"
                  )
                )

                let clientParam = makeClientParam(app, module) flatten(.out) -> <set of out>(p:
                  out = p
                )

                ParameterDeclC = [genCallback] |  clientParam flatten(.out)
              )
            )

            Result = name -> <Result> (:
              TypeName = "*GrpcServiceHandler"
            )
          )
          Block = name -> <Block>(:
            StatementList = [.] -> <sequence of StatementList>(item:
              Statement = item -> <Statement> (:
                ReturnStmt = item -> <ReturnStmt> (:
                  let clientParamList = makeClientParamList(app, module) flatten(.out) -> <set of out>(pl:
                    out = pl
                  )
                  let unimpl = "&(" + grpcPkgName + ".Unimplemented" + app.name + "Server{})"
                  PayLoad = "&GrpcServiceHandler{" + Join(["genCallback", serviceImplVarname, unimpl] | clientParamList flatten(.out),",") + "}"
                )
              )
            )
          )
        )
      )

      let registerServer = [.name] -> <sequence of TopLevelDecl>(name:
        Comment = "// RegisterServer registers the " + name + " gRPC service"
        MethodDecl = name -> <MethodDecl>(:
          Receiver = name -> <Receiver>(:
            ReceiverType = "s *GrpcServiceHandler"
          )
          FunctionName = "RegisterServer"
          Signature = name -> <Signature> (:
            Parameters = name -> <Parameters>(:
              ParameterList = name -> <ParameterList>(:
                ParameterDecl = paramDecl("ctx", "context.Context")
                ParameterDeclC = [name] -> <sequence of ParameterDeclC>(n:
                  ParameterDecl = n -> <ParameterDecl>(:
                    Identifier = "server"
                    TypeName = "*grpc.Server"
                  )
                )
              )
            )
          )
          Block = name -> <Block>(:
            StatementList = [name] -> <sequence of StatementList>(n:
              Statement = n -> <Statement> (:
                FunctionCall = n -> <FunctionCall> (:
                  FunctionName = grpcPkgName + "." + "Register" + n + "Server"
                  FunctionArgs = n -> <FunctionArgs>(:
                    Expression = makeValueExpr("server")
                    FuncArgsRest = [n] -> <sequence of FuncArgsRest>(np:
                      Expression = makeValueExpr("s")
                    )                  
                  )
                )
              )
            )
          )
        )
      )

      Comment = "// Code generated by sysl DO NOT EDIT.\n"

      TopLevelDecl =  serviceHandlerStruct | newServiceHandler | registerServer | EpHandlerMethodDecl(app.endpoints, grpcPkgName, module)
    )

  !view getPackage(app <: sysl.App) -> string:
    app -> (:
      pname = if "package" in .attrs then if .attrs.package != null then .attrs.package else ToLower(.name) else ToLower(.name)
    )
